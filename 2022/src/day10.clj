(ns day10
  (:require [clojure.test :refer :all]
            [clojure.string :as string]))

(defn parse-instr [line]
  (let [[op & args] (string/split line #"\s")]
    (case op
      "noop" {:op     (keyword op)
              :cycles 1}
      "addx" {:op     (keyword op)
              :cycles 2
              :val    (Integer/parseInt (first args))})))

(defn parse [s]
  (->> (string/split-lines s)
       (map parse-instr)))

; ==============================================================================
; Part 1
; ==============================================================================
(defrecord Cpu [x])
(def init-cpu (->Cpu 1))

(defn step [instr cpu]
  (case (:op instr)
    :noop cpu
    :addx (update cpu :x + (:val instr))))

(defn step-history [cpu instrs]
  (if-some [{:keys [cycles] :as instr} (first instrs)]
    (->> (rest instrs)
         (step-history (step instr cpu))
         (concat (repeat cycles cpu)))
    (list cpu)))

(defn signal-strengths [cpu-history]
  (->> cpu-history
       (map-indexed (fn [cycle cpu] [(inc cycle) cpu]))     ; start count at 1
       (drop 19)
       (take-nth 40)
       (map (fn [[cycle cpu]] (* cycle (:x cpu))))))

(defn sol1 [instrs]
  (->> instrs
       (step-history init-cpu)
       signal-strengths
       (reduce +)))

(deftest part1-examples
  (let [input (parse "addx 15\naddx -11\naddx 6\naddx -3\naddx 5\naddx -1\naddx -8\naddx 13\naddx 4\nnoop\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx -35\naddx 1\naddx 24\naddx -19\naddx 1\naddx 16\naddx -11\nnoop\nnoop\naddx 21\naddx -15\nnoop\nnoop\naddx -3\naddx 9\naddx 1\naddx -3\naddx 8\naddx 1\naddx 5\nnoop\nnoop\nnoop\nnoop\nnoop\naddx -36\nnoop\naddx 1\naddx 7\nnoop\nnoop\nnoop\naddx 2\naddx 6\nnoop\nnoop\nnoop\nnoop\nnoop\naddx 1\nnoop\nnoop\naddx 7\naddx 1\nnoop\naddx -13\naddx 13\naddx 7\nnoop\naddx 1\naddx -33\nnoop\nnoop\nnoop\naddx 2\nnoop\nnoop\nnoop\naddx 8\nnoop\naddx -1\naddx 2\naddx 1\nnoop\naddx 17\naddx -9\naddx 1\naddx 1\naddx -3\naddx 11\nnoop\nnoop\naddx 1\nnoop\naddx 1\nnoop\nnoop\naddx -13\naddx -19\naddx 1\naddx 3\naddx 26\naddx -30\naddx 12\naddx -1\naddx 3\naddx 1\nnoop\nnoop\nnoop\naddx -9\naddx 18\naddx 1\naddx 2\nnoop\nnoop\naddx 9\nnoop\nnoop\nnoop\naddx -1\naddx 2\naddx -37\naddx 1\naddx 3\nnoop\naddx 15\naddx -21\naddx 22\naddx -6\naddx 1\nnoop\naddx 2\naddx 1\nnoop\naddx -10\nnoop\nnoop\naddx 20\naddx 1\naddx 2\naddx 2\naddx -6\naddx -11\nnoop\nnoop\nnoop\n")]
    (is (= (sol1 input) 13140))))

; ==============================================================================
; Part 2
; ==============================================================================
(def enumerate (partial map-indexed list))

(defn render-cpu-history [cpu-history]
  (doseq [line (partition 40 cpu-history)]
    (doseq [[x cpu] (enumerate line)]
      (if (<= (dec (:x cpu)) x (inc (:x cpu)))
        (print "#")
        (print " ")))
    (println)))

(defn sol2 [instrs]
  (->> instrs
       (step-history init-cpu)
       (render-cpu-history)))

(deftest part2-examples
  (let [input (parse "addx 15\naddx -11\naddx 6\naddx -3\naddx 5\naddx -1\naddx -8\naddx 13\naddx 4\nnoop\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx 5\naddx -1\naddx -35\naddx 1\naddx 24\naddx -19\naddx 1\naddx 16\naddx -11\nnoop\nnoop\naddx 21\naddx -15\nnoop\nnoop\naddx -3\naddx 9\naddx 1\naddx -3\naddx 8\naddx 1\naddx 5\nnoop\nnoop\nnoop\nnoop\nnoop\naddx -36\nnoop\naddx 1\naddx 7\nnoop\nnoop\nnoop\naddx 2\naddx 6\nnoop\nnoop\nnoop\nnoop\nnoop\naddx 1\nnoop\nnoop\naddx 7\naddx 1\nnoop\naddx -13\naddx 13\naddx 7\nnoop\naddx 1\naddx -33\nnoop\nnoop\nnoop\naddx 2\nnoop\nnoop\nnoop\naddx 8\nnoop\naddx -1\naddx 2\naddx 1\nnoop\naddx 17\naddx -9\naddx 1\naddx 1\naddx -3\naddx 11\nnoop\nnoop\naddx 1\nnoop\naddx 1\nnoop\nnoop\naddx -13\naddx -19\naddx 1\naddx 3\naddx 26\naddx -30\naddx 12\naddx -1\naddx 3\naddx 1\nnoop\nnoop\nnoop\naddx -9\naddx 18\naddx 1\naddx 2\nnoop\nnoop\naddx 9\nnoop\nnoop\nnoop\naddx -1\naddx 2\naddx -37\naddx 1\naddx 3\nnoop\naddx 15\naddx -21\naddx 22\naddx -6\naddx 1\nnoop\naddx 2\naddx 1\nnoop\naddx -10\nnoop\nnoop\naddx 20\naddx 1\naddx 2\naddx 2\naddx -6\naddx -11\nnoop\nnoop\nnoop\n")]
    (sol2 input)))

; ==============================================================================
; Main
; ==============================================================================
(defn -main [& args]
  (if (not= 1 (count args))
    (println "Invalid number of parameters. Expecting one input file.")
    (let [[filename] args
          input (parse (slurp filename))]
      (println "First:" (sol1 input))
      (println "Second:")
      (sol2 input))))
